plugins {
	id 'fabric-loom' version "1.10-SNAPSHOT"
	id 'maven-publish'
	id 'java'
	id 'com.gradleup.shadow' version "9.0.0-beta17"
	id 'org.jetbrains.kotlin.jvm' version "2.1.0-Beta2"
}

version = project.mod_version
group = project.maven_group

base {
	group = project.group + "." + project.mod_id
	archivesName = project.mod_file_name
	version = project.mod_version

	if (mod_version.endsWith('-dev')) {
		version += "." + new Date().format('MM.dd-HH.mm.ss')
	} else {
		version += "+" + project.minecraft_version
	}
}

repositories {
	mavenCentral()
	maven {
		url = "https://maven.fabricmc.net/"
	}
	maven {
		url = "https://repo.essential.gg/repository/maven-public"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation(include("org.jetbrains.kotlin:kotlin-stdlib:2.1.0-Beta2"))

	shadow(implementation("org.apache.commons:commons-math4-legacy:4.0-beta1"))

	implementation(include("gg.essential:vigilance:$project.vigilanceVersion"))
	implementation(include("gg.essential:elementa:$project.elementaVersion"))
	modImplementation(include("gg.essential:universalcraft-$project.minecraft_version-fabric:$project.ucVersion"))
}

loom {
	accessWidenerPath = file("src/main/resources/sbc.accesswidener")
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${base.archivesName}" }
	}
}

shadowJar {
	configurations = [project.configurations.shadow]
	archiveClassifier.set("shadow")
	relocate "org.apache.commons", "com.sbc.shadow.org.apache.commons"
}

remapJar {
	dependsOn(shadowJar)
	inputFile = tasks.shadowJar.archiveFile
}

processResources {
	inputs.property "mod_version", project.mod_version
	inputs.property "minecraft_version", project.minecraft_version

	filesMatching("fabric.mod.json") {
		expand "mod_version": project.mod_version + "+" + project.minecraft_version
	}
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java', 'src/main/kotlin']
		}
		resources {
			srcDirs = ['src/main/resources']
		}
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}


publishing {
	publications {
		create("mavenJava", MavenPublication) {
			from components.java
		}
	}
	repositories {
		// published repos
	}
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
	kotlinOptions {
		jvmTarget = "17"
	}
}

def generatedConstantsDir = "$buildDir/generated/sources/constants"
sourceSets.main.java.srcDirs += generatedConstantsDir

tasks.register("generateModConstants") {
	doLast {
		def outputDir = file(generatedConstantsDir + "/com/sbc/constants")
		outputDir.mkdirs()
		def file = new File(outputDir, "ModInfo.java")
		file.text = """
            package com.sbc.data;

            public class ModInfo {
                public static final String VERSION = "${project.version}";
                public static final boolean DEV = VERSION.contains("-dev");
            }
        """.stripIndent()
	}
}

compileJava.dependsOn(tasks.generateModConstants)

apply plugin: 'idea'
apply plugin: 'kotlin'